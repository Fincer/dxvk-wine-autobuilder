# Maintainer: Adrià Cereto i Massagué <ssorgatem at gmail.com>

pkgname=dxvk-git
pkgver=20180814.e5eb155
pkgrel=1
pkgdesc="A Vulkan-based compatibility layer for Direct3D 11 which allows running 3D applications on Linux using Wine."
arch=('x86_64')
url="https://github.com/doitsujin/dxvk"
license=('zlib/libpng')
depends=('vulkan-icd-loader')
makedepends=('ninja' 'meson>=0.43' 'glslang' 'mingw-w64-gcc' 'git')
provides=("dxvk")
conflicts=("dxvk-bin")
options=(!strip !buildflags staticlibs)
source=($pkgname::"git+https://github.com/doitsujin/dxvk.git"
patch_nowine.patch)
#patch_revert-attribute_divisor.patch)
md5sums=('SKIP'
         'e44502b2fc7e2db2db0090f779531189')

pkgver() {
        cd "$pkgname"
        git log -1 --format=%cd.%h --date=short|tr -d -
}

prepare() {
    cd "$pkgname"
    patch -p1 < ../patch_nowine.patch
    #patch -p1 -R < ../patch_revert-attribute_divisor.patch
}

build() {
    "$pkgname"/package-release.sh $pkgver $PWD --no-package
}

package() {
    mkdir -p "$pkgdir/usr/share/dxvk"
    cp -rv dxvk-$pkgver/* "$pkgdir/usr/share/dxvk"
    if [ ! -f "$pkgdir"/usr/share/dxvk/x64/d3d11.dll ] ||\
     [ ! -f "$pkgdir"/usr/share/dxvk/x64/dxgi.dll ] ||\
     [ ! -f "$pkgdir"/usr/share/dxvk/x32/d3d11.dll ] ||\
     [ ! -f "$pkgdir"/usr/share/dxvk/x32/dxgi.dll ]; then
        echo "Missing files, build was unsuccessful"
        return 1
    fi
        mkdir -p "$pkgdir/usr/bin"
        ln -s "/usr/share/dxvk/x32/setup_dxvk.sh" "$pkgdir/usr/bin/setup_dxvk32"
        ln -s "/usr/share/dxvk/x64/setup_dxvk.sh" "$pkgdir/usr/bin/setup_dxvk64"
}
